Процедура ДополнитьФорму(Форма) Экспорт
	// {{ Малышева И.Н., ДЗ "Внесение изменений в конфигурацию на поддержке", добавить КонтактноеЛицо, СогласованнаяСкидка на форму
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьКнопкуСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);   
	КонецЕсли;
	// }}
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	// {{ Малышева И.Н., ДЗ "Внесение изменений в конфигурацию на поддержке", добавить КонтактноеЛицо, СогласованнаяСкидка на форму
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	// }}
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	// {{ Малышева И.Н., ДЗ "Внесение изменений в конфигурацию на поддержке", добавить КонтактноеЛицо, СогласованнаяСкидка на форму
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	// }}
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	// {{ Малышева И.Н., ДЗ "Внесение изменений в конфигурацию на поддержке", добавить КонтактноеЛицо, СогласованнаяСкидка на форму
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	// }}
КонецПроцедуры 

Процедура ДобавитьКнопкуСогласованнаяСкидкаВГруппаШапкаЛево(Форма)
	// {{ Малышева И.Н., ДЗ "Внесение изменений в конфигурацию на поддержке", добавить КонтактноеЛицо, СогласованнаяСкидка на форму
	ГруппаСкидкаИКоманда = Форма.Элементы.Добавить("ГруппаСкидкаИКоманда", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидкаИКоманда.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидкаИКоманда.ОтображатьЗаголовок = Ложь;
	ГруппаСкидкаИКоманда.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;  
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидкаИКоманда);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка"; 
	ПолеВвода.МаксимальноеЗначение = 100;
	ПолеВвода.УстановитьДействие("ПриИзменении","ДЗ_СогласованнаяСкидкаПриИзменении");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "ПересчитатьТаблицу"; 
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидкаИКоманда);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	// }}
КонецПроцедуры